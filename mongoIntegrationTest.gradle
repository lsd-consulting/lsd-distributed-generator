sourceSets {
    mongoIntegrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/mongoIntegrationTest/java')
        }
    }
}

configurations {
    mongoIntegrationTestImplementation.extendsFrom implementation
}

dependencies {
    // Spring
    mongoIntegrationTestImplementation("org.springframework.boot:spring-boot-starter-web")
    mongoIntegrationTestImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "junit", module: "junit"
        exclude group: "org.mockito", module: "mockito-core"
    }

    // Lsd
    mongoIntegrationTestImplementation 'io.github.lsd-consulting:lsd-distributed-mongodb-connector:5.0.0'

    // jUnit
    mongoIntegrationTestImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'

    // Db
//    mongoIntegrationTestImplementation "io.zonky.test:embedded-database-spring-test:2.3.0"
//    mongoIntegrationTestImplementation "io.zonky.test:embedded-postgres:2.0.4"
    mongoIntegrationTestImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.5.4'

    // Other
    mongoIntegrationTestImplementation 'io.mockk:mockk-jvm:1.13.5'
    mongoIntegrationTestImplementation 'org.mockito:mockito-core:5.4.0'

}

tasks.register('mongoIntegrationTest', Test) {
    useJUnitPlatform()
    dependsOn assemble

    testClassesDirs = sourceSets.mongoIntegrationTest.output.classesDirs
    classpath = sourceSets.mongoIntegrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    testLogging.showStandardStreams = true
    mustRunAfter(test)
}

check.dependsOn(mongoIntegrationTest)

mongoIntegrationTest {
    afterSuite { desc, result ->
        if (!desc.parent) {
            logger.lifecycle("Mongo integration test results: (${result.testCount} features, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)")
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}
